#!/usr/bin/env coffee
logger = require 'torch'
optimist = require 'optimist'

core = require '..'


options =
  # Should correspond to an installed NPM module named 'axiom-<moduleName>'
  'm':
    alias: ['axiom.moduleName']
    demand: true
  's':
    alias: ['axiom.serviceName']
    demand: true
  # The contents of 'data' are really the 'args' we expect to passs
  'data':
    default: {}


main = ->
  # Extract the arguments we expect, and remaining args for the service.
  parsed = optimist.options options
  {axiom, data} = parsed.argv
  {moduleName, serviceName} = axiom

  # Initialize Axiom, load the module.
  config = process.env['AXIOM.config'] or {}
  modules = []
  modules.push moduleName

  core.init config, modules

  channel = [moduleName, serviceName].join('.')
  core.request channel, data, (err, results) ->
    if err
      logger.red err.stack
    else
      logger.green {results}


if module is require.main
  if process.env['axiom.env'] is 'testing'
    mockery = require 'mockery'
    mockery.enable
      warnOnReplace: false,
      warnOnUnregistered: false

    mockery.registerMock 'axiom-base',
      services:
        echo: (args, next) ->
          next null, args

  main()


module.exports = main
