#!/usr/bin/env coffee
logger = require 'torch'
optimist = require 'optimist'

core = require '..'


options =
  # Should correspond to an installed NPM module named 'axiom-<moduleName>'
  'moduleName':
    alias: ['module', 'm']
    demand: true
  'serviceName':
    alias: ['service', 's']
    demand: true
  # The contents of 'data' are really the 'args' we expect to passs
  'data':
    default: {}


main = ->
  # Extract the arguments we expect, and remaining args for the service.
  parsed = optimist.options options
  {moduleName, serviceName, data} = parsed.argv

  # Initialize Axiom, load the module.
  config = process.env['AXIOM.config'] or {}
  config.modules or= []
  config.modules.push moduleName

  core.init config

  channel = [moduleName, serviceName].join('.')
  core.request channel, data, (err, results) ->
    if err
      logger '[failure]'.red, {err}
    else
      logger '[success]'.green, {results}


if module is require.main
  main()


module.exports = main
